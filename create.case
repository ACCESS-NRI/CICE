#!/bin/csh -f

echo " "
echo "${0}:"

set CICE_SANDBOX = `pwd`
set CICE_SCRDIR = ${CICE_SANDBOX}/configuration/scripts
set initargv = ( $argv[*] ) 

set helpheader = 0
set dash = "-"
set spval = "UnDeFiNeD"
set mach = $spval
set case = $spval
set test = $spval
set grid = gx3
set pesx = 4x1
set sets = ""
set bdir = $spval
set testid = $spval
set testsuite = $spval
set baseCom = $spval  # Baseline compare
set baseGen = $spval  # Baseline generate

if ($#argv < 1) then
  set helpheader = 1
endif

# check for -h
while (1)
  if ($#argv < 1) break;
  if ("$argv[1]" =~ ${dash}h* ) then
    set helpheader = 1
  endif
  shift argv
end

#------------------------------------------------------------
# Help output

if ( $helpheader ) then
cat << EOF1

NAME   
      create.case - creates a CICE case directory
        -h help
        -c case, case directory/name
        -m machine, machine name (required)
        -p tasks x threads, mxn (default is 4x1)
        -g grid, grid (default = gx3)
        -s build and namelist mod files, comma separated with no spaces (default = " ")

        -- For testing purposes --
        -t test, test name (cannot be selected if -c is selected)
           Available tests:
             - "smoke": Compiles code and performs 1 day run
             - "10day": Compiles code and performs 10 day run
             - "annual": Compiles code and performs annual run
             - "restart": Compiles code and performs an exact restart test
        -bd baseline directory, high level directory where baselines are stored
        -bg cice version name for generate, directory under the baseline directory where baselines are stored
        -bc cice version name for compare, directory under the baseline directory where baselines are stored
        -testid test ID, user-defined indentifier for test (default = t00)
        -ts test suite, pre-defined set of tests to run.  Current set of predefined tests:
             - "base_suite": runs the smoke test (with debug), 10day test, annual test, and restart test

      EXAMPLES:
      create.case -h
      create.case -c ~/caseA -m yellowstone 
      create.case -p 32x4 -g gx1 -m gordon -c caseB
      create.case -c ~/caseC -m spirit -s diag1,writeicf
   
      For testing instructions, please view README.test

EOF1
exit -1
endif

#------------------------------------------------------------
# Read in command line arguments

set argv = ( $initargv[*] )

while (1)
  if ( $#argv < 1 ) break;
  set option = $argv[1];
  shift argv
  if ( $#argv < 1 ) then
    echo "${0}: ERROR1 in $option"
    exit -1
  endif
  if ($argv[1] =~ $dash* ) then
    echo "${0}: ERROR2 in $option"
    exit -1
  endif

  switch ( $option )
    case "-m":
      set mach = $argv[1]
      breaksw
    case "-c":
      set case = $argv[1]
      breaksw
    case "-t":
      set test = $argv[1]
      breaksw
    case "-g":
      set grid = $argv[1]
      breaksw
    case "-p":
      set pesx = $argv[1]
      breaksw
    case "-s":
      set sets = $argv[1]
      breaksw
    case "-bd":
      set bdir = $argv[1]
      breaksw
    case "-bc":
      set baseCom = $argv[1]
      breaksw
    case "-bg":
      set baseGen = $argv[1]
      breaksw
    case "-ts":
      set testsuite = $argv[1]
      breaksw
    case "-testid":
      set testid = $argv[1]
      breaksw
    default:
      echo "${0}: ERROR unknown option $option, use -h for help"
      exit -1
      breaksw
  endsw
  shift argv
end

if ($mach == $spval) then
  echo "${0}: ERROR in arguments, -m required"
  exit -1
endif

if ($case == $spval && $test == $spval && $testsuite == $spval) then
  echo "${0}: ERROR in arguments, -c, -t, or -ts required"
  exit -1
endif

if ($case != $spval && $test != $spval) then
  echo "${0}: ERROR in arguments, cannot use both -c and -t"
  exit -1
endif

if ($case != $spval && $testsuite != $spval) then
  echo "${0}: ERROR in arguments, cannot use both -c and -ts"
  exit -1
endif

if ($testsuite != $spval && $test != $spval) then
  echo "${0}: ERROR in arguments, cannot use both -ts and -t"
  exit -1
endif

if ($test != $spval && $test != 'smoke' && $test != '10day' && $test != 'annual' \
 && $test != 'restart') then
  echo "${0}: ERROR in arguments.  $test is not a valid test"
  exit -1
endif

if (($testsuite != $spval || $test != $spval) && $testid == $spval) then
  echo "${0}: ERROR in arguments.  -testid must be passed if using -ts or -t"
  exit -1
endif

set chck = `echo ${pesx} | sed 's/^[0-9]\+x[0-9]\+$/OK/'`
if (${chck} == OK) then
  set task = `echo ${pesx} | sed s/x.\*//`
  set thrd = `echo ${pesx} | sed s/.\*x//`
else
  echo "${0}: ERROR in -p argument, ${pesx}, must be mxn"
  exit -1
endif

# Check to see if this is a test-suite run.  If so, loop through the various
#   tests and create a separate folder for each
if ( $testsuite != $spval ) then
  set tsdir = "${testsuite}.${testid}"
  set tsfile = "${CICE_SCRDIR}/tests/${testsuite}.ts"
  set first_test = 1   # For creating suite.run
  foreach line ( "`cat $tsfile`" )
    # Check if line is a comment line
    if ( $line:q =~ '#'* || $line:q =~ '$'* || $line:q =~ '!'* ) then
      echo "skipping line: $line"
      continue
    endif
    # Obtain the test name, sets, grid, and PE information from .ts file
    set test = `echo $line | cut -d' ' -f1`
    set grid = `echo $line | cut -d' ' -f2`
    set pesx = `echo $line | cut -d' ' -f3`
    set sets_tmp = `echo $line | cut -d' ' -f4`
    # Create a new sets_base variable to store sets passed to create.case
    if (! $?sets_base ) then
      set sets_base = "$sets"
    endif
    # Append sets from .ts file to the $sets variable
    if ( $sets_tmp == "none" ) then
      set sets = "$sets_base"
    else
      if ( $sets_base == "" ) then
        set sets = "$sets_tmp"
      else
        set sets = "$sets_base,$sets_tmp"
        # Remove duplictes in the sets variable
        set sets = "`echo $sets | sed -e 's/\b\([a-z]\+\)[ ,\n]\1/\1/g'`"
      endif
    endif
endif

if ($case =~ $spval) then
  if ($sets != "") then
    set soptions = `echo $sets | sed 's/,/_/g'`
    # Only include $testid in testname if this is not a baseline-generating run
    set testname_noid = "${test}_${grid}_${mach}_${pesx}_${soptions}"
    set testname_base = "${test}_${grid}_${mach}_${pesx}_${soptions}.${testid}"
  else
    set testname_noid = "${test}_${grid}_${mach}_${pesx}"
    set testname_base = "${test}_${grid}_${mach}_${pesx}.${testid}"
  endif
  if ($testsuite != $spval) then
    set testname = "${tsdir}/$testname_base"
  else
    set testname = "$testname_base"
  endif
  set case = ${testname}
endif

if (-d $case) then
  echo "${0}: ERROR, case $case already exists"
  exit -1
endif
mkdir -p $case
echo "`date`${0} $initargv[*]" > $case/README.case

#------------------------------------------------------------
# Setup case directory, copy files to case directory

cd ${case}
set casedir = `pwd`
set casescr = "${casedir}/casescripts"
if !( -d ${casescr}) mkdir ${casescr}

# from basic script dir to case
foreach file (cice.build cice.settings Makefile ice_in makdep.c)
  if !(-e ${CICE_SCRDIR}/$file) then
    echo "${0}: ERROR, ${CICE_SCRDIR}/$file not found"
    exit -1
  endif
  cp -f -p ${CICE_SCRDIR}/$file ${casedir}
end

# from machines dir to case
foreach file (env.${mach} Macros.${mach})
  if !(-e ${CICE_SCRDIR}/machines/$file) then
    echo "${0}: ERROR, ${CICE_SCRDIR}/machines/$file not found"
    exit -1
  endif
  cp -f -p ${CICE_SCRDIR}/machines/$file ${casedir}
end

# For test cases, copy extra namelist modification files to casescr
cp -f -p $CICE_SCRDIR/options/test_nml.${test}* ${casescr}/

# from basic script dir to casescr
foreach file (parse_namelist.sh parse_settings.sh cice_decomp.csh cice.run.setup.csh)
  if !(-e ${CICE_SCRDIR}/$file) then
    echo "${0}: ERROR, ${CICE_SCRDIR}/$file not found"
    exit -1
  endif
  cp -f -p ${CICE_SCRDIR}/$file ${casescr}
end

if ($case =~ */*) then
  set casename = $case:t
else
  set casename = $case
endif

cd ${casedir}
source ./env.${mach} || exit 2

echo CICE_SANDBOX  = ${CICE_SANDBOX}
echo CICE_CASENAME = ${casename}
echo CICE_CASEDIR  = ${casedir}
echo CICE_MACHINE  = ${mach}

#------------------------------------------------------------
# Compute a default blocksize

setenv CICE_DECOMP_GRID  ${grid}
setenv CICE_DECOMP_NTASK ${task}
setenv CICE_DECOMP_NTHRD ${thrd}

source ${casescr}/cice_decomp.csh

#------------------------------------------------------------
# Copy in and update cice.settings and ice_in files

set fimods = ${casescr}/ice_in.mods
set fsmods = ${casescr}/cice.settings.mods

cp ice_in ${casescr}/ice_in.base
cp cice.settings ${casescr}/cice.settings.base
if (-e ${fimods}) rm ${fimods}
if (-e ${fsmods}) rm ${fsmods}

cat >! ${fimods} << EOF1
nprocs = ${task}
atm_data_dir = '${CICE_MACHINE_INPUTDATA}/'
distribution_type = '${CICE_DECOMP_DECOMP}'
processor_shape   = '${CICE_DECOMP_DSHAPE}'
ice_ic            = '${CICE_DECOMP_INITFILE}'
grid_file         = '${CICE_DECOMP_GRIDFILE}'
kmt_file          = '${CICE_DECOMP_KMTFILE}'
EOF1

# If this is a baseline-compare test, modify CICE_RUNDIR
if ($bdir != $spval) then
  setenv basedir_tmp ${bdir}
else
  setenv basedir_tmp ${CICE_MACHINE_BASELINE}
endif

cat >! ${fsmods} << EOF1
setenv CICE_SANDBOX  ${CICE_SANDBOX}
setenv CICE_SCRDIR   ${CICE_SCRDIR}
setenv CICE_CASENAME ${casename}
setenv CICE_CASEDIR  ${casedir}
setenv CICE_MACHINE  ${mach}
setenv CICE_RUNDIR   ${CICE_MACHINE_WKDIR}/${casename}
setenv CICE_GRID     ${grid}
setenv CICE_NXGLOB   ${CICE_DECOMP_NXGLOB}
setenv CICE_NYGLOB   ${CICE_DECOMP_NYGLOB}
setenv CICE_NTASKS   ${task}
setenv CICE_NTHRDS   ${thrd}
setenv CICE_DECOMP   ${CICE_DECOMP_DECOMP}
setenv CICE_DSHAPE   ${CICE_DECOMP_DSHAPE}
setenv CICE_MXBLCKS  ${CICE_DECOMP_MXBLCKS}
setenv CICE_BLCKX    ${CICE_DECOMP_BLCKX}
setenv CICE_BLCKY    ${CICE_DECOMP_BLCKY}
setenv CICE_RSTPFILE ${CICE_DECOMP_RSTPFILE}
setenv CICE_BASELINE ${basedir_tmp}
setenv CICE_BASEGEN  ${baseGen}
setenv CICE_BASECOM  ${baseCom}
setenv CICE_SPVAL    ${spval}
EOF1

if ($test != $spval) then
  echo "setenv CICE_TEST     ${test}" >> ${fsmods}
  echo "setenv CICE_TESTNAME ${testname_noid}" >> ${fsmods}
else
  echo "setenv CICE_TEST     ${spval}" >> ${fsmods}
  echo "setenv CICE_TESTNAME ${spval}" >> ${fsmods}
endif

if ($#sets > 0) then
set setssplit = `echo $sets | sed 's/,/ /g'`
foreach name ($setssplit)
  set found = 0
  if (-e ${CICE_SCRDIR}/options/set_nml.${name}) then
    cat ${CICE_SCRDIR}/options/set_nml.${name} >> ${fimods}
    echo "adding namelist mods set_nml.${name}"
    set found = 1
  endif
  if (-e ${CICE_SCRDIR}/options/set_env.${name}) then
    cat ${CICE_SCRDIR}/options/set_env.${name} >> ${fsmods}
    echo "adding env mods set_env.${name}"
    set found = 1
  endif
  if (${found} == 0) then
    echo "${0}: ERROR, ${CICE_SCRDIR}/options/set_[nml,env].${name} not found"
    exit -1
  endif
end
endif

if ($test != $spval) then
  if (-e ${CICE_SCRDIR}/options/test_nml.${test}) then
    cat ${CICE_SCRDIR}/options/test_nml.${test} >> ${fimods}
    echo "adding namelist mods test_nml.${test}"
    set found = 1
  endif
  if (${found} == 0) then
    echo "${0}: ERROR, ${CICE_SCRDIR}/options/test_nml.${name} not found"
    exit -1
  endif
endif

${casescr}/parse_settings.sh cice.settings ${fsmods}
${casescr}/parse_namelist.sh ice_in ${fimods}

#------------------------------------------------------------
# Generate run script

source ./cice.settings
source ./env.${mach} || exit 2

${casescr}/cice.run.setup.csh

#------------------------------------------------------------

if ($test != $spval) then
  # Print information to stdout
  echo "Creating scripts for $test test"

  # Generate test script
  ${CICE_SCRDIR}/tests/cice.test.csh 
endif

if ( $testsuite != $spval ) then
    cd $CICE_SANDBOX
    # Write build and run commands to suite.run
    if ( $first_test == 1 ) then
      set first_test = 0
cat >! ./${tsdir}/suite.run << EOF0
#!/bin/csh -f
EOF0
cat >! ./${tsdir}/results.csh << EOF0
#!/bin/csh -f
cat $testname_base/test_output > results.log
EOF0
      chmod +x ./${tsdir}/suite.run
      chmod +x ./${tsdir}/results.csh
    else
cat >> ./${tsdir}/results.csh << EOF
cat $testname_base/test_output >> results.log
EOF
    endif
cat >> ./${tsdir}/suite.run << EOF
cd $testname_base
./cice.build
./cice.submit
cd ..
EOF
    
    # Reset case for the next test in suite
    set case = $spval

    echo "" 
    echo "---" 
    echo "" 
  end

  # Add code to results.csh to count the number of failures
cat >> ./${tsdir}/results.csh << EOF
cat ./results.log | grep FAIL
set failures = \`cat ./results.log | grep FAIL | wc -l\`
set success = \`cat ./results.log | grep PASS | wc -l\`
set total = \`cat ./results.log | wc -l\`
echo ""
echo "\$success of \$total tests PASSED"
echo "\$failures of \$total tests FAILED"
EOF
endif

echo " "
echo "${0} done"
echo " "

