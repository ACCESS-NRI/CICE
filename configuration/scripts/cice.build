#! /bin/csh -f

source ./cice.settings
source ${CICE_CASEDIR}/env.${CICE_MACHINE} || exit 2

if (${CICE_MACHINE} != ${CICE_MACHINE_ENVNAME}) then
  echo "WARNING, is CICE_MACHINE setting OK, ${CICE_MACHINE}, ${CICE_MACHINE_ENV}"
endif

echo " "
echo "${0}:"

set  stamp   = `date '+%y%m%d-%H%M%S'`
set CICE_BLDLOG_FILE = "cice.bldlog.${stamp}"

if (${CICE_CLEANBUILD} == 'true') then
  echo "cleaning objdir"
  rm -r -f ${CICE_OBJDIR}
endif
if !(-d ${CICE_OBJDIR}) mkdir -p ${CICE_OBJDIR}
cd ${CICE_OBJDIR}

setenv CICE_CPPDEFS "-DNXGLOB=${CICE_NXGLOB} -DNYGLOB=${CICE_NYGLOB} -DBLCKX=${CICE_BLCKX} -DBLCKY=${CICE_BLCKY} -DMXBLCKS=${CICE_MXBLCKS} -DNICELYR=${NICELYR} -DNSNWLYR=${NSNWLYR} -DNICECAT=${NICECAT} -DTRAGE=${TRAGE} -DTRFY=${TRFY} -DTRLVL=${TRLVL} -DTRPND=${TRPND} -DTRBRI=${TRBRI} -DNTRAERO=${NTRAERO}  -DTRZS=${TRZS} -DNBGCLYR=${NBGCLYR} -DTRALG=${TRALG} -DTRBGCZ=${TRBGCZ} -DTRDOC=${TRDOC} -DTRDOC=${TRDOC} -DTRDIC=${TRDIC} -DTRDON=${TRDON} -DTRFED=${TRFED} -DTRFEP=${TRFEP} -DTRZAERO=${TRZAERO} -DTRBGCS=${TRBGCS} -DNUMIN=${NUMIN} -DNUMAX=${NUMAX}"

if ($DITTO == 'yes') then
  setenv CICE_CPPDEFS "${CICE_CPPDEFS} -DREPRODUCIBLE"
endif

if (${CICE_NTASKS} == 1) then
   setenv CICE_COMMDIR serial
else
   setenv CICE_COMMDIR mpi 
endif

if (${CICE_IOTYPE} == 'netcdf') then
  set IODIR = io_netcdf
  setenv CICE_CPPDEFS "${CICE_CPPDEFS} -Dncdf"
else if (${CICE_IOTYPE} == 'pio') then
  set IODIR = io_pio
  setenv CICE_CPPDEFS "${CICE_CPPDEFS} -Dncdf"
else
  set IODIR = io_binary
endif

### List of source code directories (in order of importance).
cat >! Filepath << EOF
${CICE_SANDBOX}/cicecore/drivers/${CICE_DRVOPT}
${CICE_SANDBOX}/cicecore/cicedynB/dynamics
${CICE_SANDBOX}/cicecore/cicedynB/general
${CICE_SANDBOX}/cicecore/cicedynB/analysis
${CICE_SANDBOX}/cicecore/cicedynB/infrastructure
${CICE_SANDBOX}/cicecore/cicedynB/infrastructure/io/$IODIR
${CICE_SANDBOX}/cicecore/cicedynB/infrastructure/comm/${CICE_COMMDIR}
${CICE_SANDBOX}/cicecore/shared
${CICE_SANDBOX}/cicecore/shared/constants/${CICE_CONSTOPT}
${CICE_SANDBOX}/icepack/columnphysics
${CICE_SANDBOX}/icepack/columnphysics/constants/${CICE_CONSTOPT}
EOF

echo " "
echo CICE_GRID     = ${CICE_GRID}
echo CICE_NTASK    = ${CICE_NTASKS}
echo CICE_NTHRD    = ${CICE_NTHRDS}
echo "global N, block_size" 
echo "x    ${CICE_NXGLOB},    ${CICE_BLCKX}"
echo "y    ${CICE_NYGLOB},    ${CICE_BLCKY}"
echo "max_blocks = ${CICE_MXBLCKS}"
echo "CICE_CPPDEFS = ${CICE_CPPDEFS}"
echo "Filepath = "
cat ${CICE_OBJDIR}/Filepath
echo " "

echo "building makdep"
cc -o makdep ${CICE_CASEDIR}/makdep.c     || exit 2

echo "building cice > ${CICE_OBJDIR}/${CICE_BLDLOG_FILE}"
if !(-d ${CICE_RUNDIR}) mkdir -p ${CICE_RUNDIR}
if !($?CICE_MACHINE_BLDTHRDS) then
  set CICE_MACHINE_BLDTHRDS = 1
endif
if (-e ${CICE_BLDLOG_FILE}) rm ${CICE_BLDLOG_FILE}
gmake -j ${CICE_MACHINE_BLDTHRDS} VPFILE=Filepath EXEC=${CICE_RUNDIR}/cice CICE_CPPDEFSDEFS="${CICE_CPPDEFS}" \
      -f  ${CICE_CASEDIR}/Makefile MACFILE=${CICE_CASEDIR}/Macros.${CICE_MACHINE} | tee ${CICE_BLDLOG_FILE} 
if ($status != 0) then
  echo "${0}: COMPILE FAILED, see"
  echo "  cat ${CICE_OBJDIR}/${CICE_BLDLOG_FILE}"
  if ( ${CICE_TEST} != ${CICE_SPVAL} ) then
    # This is a test case.  Write output to test_output file
    echo "FAIL ${CICE_TESTNAME} build" >> ${CICE_CASEDIR}/test_output
  endif
  exit 99
endif

if !(-d ${CICE_LOGDIR}) mkdir -p ${CICE_LOGDIR}
cp -p ${CICE_BLDLOG_FILE} ${CICE_LOGDIR}/
echo "`date` ${0}:${CICE_CASENAME} build completed ${CICE_BLDLOG_FILE}"  >> ${CICE_CASEDIR}/README.case
echo "${0}: COMPILE SUCCESSFUL, ${CICE_LOGDIR}/${CICE_BLDLOG_FILE}"
if ( ${CICE_TEST} != ${CICE_SPVAL} ) then
  echo "PASS ${CICE_TESTNAME} build" >> ${CICE_CASEDIR}/test_output
endif

