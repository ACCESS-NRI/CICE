
#-----------------------------------------------------------
# Run the CICE model for the 2-day simulation

./cice.run

if ( $? != 0 ) then
  echo "FAIL ${CICE_TESTNAME} 2-day-run" >> ${CICE_CASEDIR}/test_output
  exit 99
else
  echo "PASS ${CICE_TESTNAME} 2-day-run" >> ${CICE_CASEDIR}/test_output
endif

# Prepend 'baseline_' to the final restart file to save for comparison
foreach file (${CICE_RUNDIR}/restart/*)
  set test_data = $file
end
set baseline_data = ${CICE_RUNDIR}/restart/baseline_$test_data:t
mv $test_data $baseline_data

#-----------------------------------------------------------
# Run the CICE model for the restart simulation

# Modify the contents of the pointer file for restart
perl -i -pe's/(\d{4})-(\d{2})-(\d{2})/sprintf("%04d-%02d-%02d",$1,$2,$3-1)/e' ${CICE_RUNDIR}/ice.restart_file

# Modify the namelist for the restart simulation
cp ice_in ice_in.2-day
${CICE_CASEDIR}/casescripts/parse_namelist.sh ice_in ${CICE_CASEDIR}/casescripts/test_nml.restart2

./cice.run
if ( $? != 0 ) then
  cp ice_in ice_in.restart
  cp ice_in.2-day ice_in
  echo "FAIL ${CICE_TESTNAME} restart-run" >> ${CICE_CASEDIR}/test_output
  exit 99
else
  echo "PASS ${CICE_TESTNAME} restart-run" >> ${CICE_CASEDIR}/test_output
endif

cp ice_in ice_in.restart
cp ice_in.2-day ice_in

#-----------------------------------------------------------

echo "Performing binary comparison between files:"
echo "baseline: $baseline_data"
echo "test:     $test_data"
if ( { cmp -s $test_data $baseline_data } ) then
  echo "PASS ${CICE_TESTNAME} exact restart" >> ${CICE_CASEDIR}/test_output
else
  echo "FAIL ${CICE_TESTNAME} exact restart" >> ${CICE_CASEDIR}/test_output
endif

#-----------------------------------------------------------

